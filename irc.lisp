(defpackage #:lrssl
  (:use #:cl #:sys.net)
  (:export lrssl))

(in-package #:lrssl)

(defparameter *numeric-replies*
  '((401 :err-no-such-nick)
    (402 :err-no-such-server)
    (403 :err-no-such-channel)
    (404 :err-cannot-send-to-channel)
    (405 :err-too-many-channels)
    (406 :err-was-no-such-nick)
    (407 :err-too-many-targets)
    (409 :err-no-origin)
    (411 :err-no-recipient)
    (412 :err-no-text-to-send)
    (413 :err-no-toplevel)
    (414 :err-wild-toplevel)
    (421 :err-unknown-command)
    (422 :err-no-motd)
    (423 :err-no-admin-info)
    (424 :err-file-error)
    (431 :err-no-nickname-given)
    (432 :err-erroneus-nickname)
    (433 :err-nickname-in-use)
    (436 :err-nick-collision)
    (441 :err-user-not-in-channel)
    (442 :err-not-on-channel)
    (443 :err-user-on-channel)
    (444 :err-no-login)
    (445 :err-summon-disabled)
    (446 :err-users-disabled)
    (451 :err-not-registered)
    (461 :err-need-more-params)
    (462 :err-already-registred)
    (463 :err-no-perm-for-host)
    (464 :err-password-mismatch)
    (465 :err-youre-banned-creep)
    (467 :err-key-set)
    (471 :err-channel-is-full)
    (472 :err-unknown-mode)
    (473 :err-invite-only-chan)
    (474 :err-banned-from-chan)
    (475 :err-bad-channel-key)
    (481 :err-no-privileges)
    (482 :err-chanop-privs-needed)
    (483 :err-cant-kill-server)
    (491 :err-no-oper-host)
    (501 :err-umode-unknown-flag)
    (502 :err-users-dont-match)
    (300 :rpl-none)
    (302 :rpl-userhost)
    (303 :rpl-ison)
    (301 :rpl-away)
    (305 :rpl-unaway)
    (306 :rpl-nowaway)
    (311 :rpl-whoisuser)
    (312 :rpl-whoisserver)
    (313 :rpl-whoisoperator)
    (317 :rpl-whoisidle)
    (318 :rpl-endofwhois)
    (319 :rpl-whoischannels)
    (314 :rpl-whowasuser)
    (369 :rpl-endofwhowas)
    (321 :rpl-liststart)
    (322 :rpl-list)
    (323 :rpl-listend)
    (324 :rpl-channelmodeis)
    (331 :rpl-notopic)
    (332 :rpl-topic)
    (333 :rpl-topic-time)
    (341 :rpl-inviting)
    (342 :rpl-summoning)
    (351 :rpl-version)
    (352 :rpl-whoreply)
    (315 :rpl-endofwho)
    (353 :rpl-namreply)
    (366 :rpl-endofnames)
    (364 :rpl-links)
    (365 :rpl-endoflinks)
    (367 :rpl-banlist)
    (368 :rpl-endofbanlist)
    (371 :rpl-info)
    (374 :rpl-endofinfo)
    (375 :rpl-motdstart)
    (372 :rpl-motd)
    (376 :rpl-endofmotd)
    (381 :rpl-youreoper)
    (382 :rpl-rehashing)
    (391 :rpl-time)
    (392 :rpl-usersstart)
    (393 :rpl-users)
    (394 :rpl-endofusers)
    (395 :rpl-nousers)
    (200 :rpl-tracelink)
    (201 :rpl-traceconnecting)
    (202 :rpl-tracehandshake)
    (203 :rpl-traceunknown)
    (204 :rpl-traceoperator)
    (205 :rpl-traceuser)
    (206 :rpl-traceserver)
    (208 :rpl-tracenewtype)
    (261 :rpl-tracelog)
    (211 :rpl-statslinkinfo)
    (212 :rpl-statscommands)
    (213 :rpl-statscline)
    (214 :rpl-statsnline)
    (215 :rpl-statsiline)
    (216 :rpl-statskline)
    (218 :rpl-statsyline)
    (219 :rpl-endofstats)
    (241 :rpl-statslline)
    (242 :rpl-statsuptime)
    (243 :rpl-statsoline)
    (244 :rpl-statshline)
    (221 :rpl-umodeis)
    (251 :rpl-luserclient)
    (252 :rpl-luserop)
    (253 :rpl-luserunknown)
    (254 :rpl-luserchannels)
    (255 :rpl-luserme)
    (256 :rpl-adminme)
    (257 :rpl-adminloc1)
    (258 :rpl-adminloc2)
    (259 :rpl-adminemail)))

(defun decode-command (line)
  "Explode a line into (prefix command parameters...)."
  ;; <message>  ::= [':' <prefix> <SPACE> ] <command> <params> <crlf>
  ;; <prefix>   ::= <servername> | <nick> [ '!' <user> ] [ '@' <host> ]
  ;; <command>  ::= <letter> { <letter> } | <number> <number> <number>
  ;; <SPACE>    ::= ' ' { ' ' }
  ;; <params>   ::= <SPACE> [ ':' <trailing> | <middle> <params> ]
  (let ((prefix nil)
        (offset 0)
        (command nil)
        (parameters '()))
    (when (and (not (zerop (length line)))
               (eql (char line 0) #\:))
      ;; Prefix present, read up to a space.
      (do () ((or (>= offset (length line))
                  (eql (char line offset) #\Space)))
        (incf offset))
      (setf prefix (subseq line 1 offset)))
    ;; Eat leading spaces.
    (do () ((or (>= offset (length line))
                (not (eql (char line offset) #\Space))))
      (incf offset))
    ;; Parse a command, reading until space or the end.
    (let ((start offset))
      (do () ((or (>= offset (length line))
                  (eql (char line offset) #\Space)))
        (incf offset))
      (setf command (subseq line start offset)))
    (when (and (= (length command) 3)
               (every (lambda (x) (find x "1234567890")) command))
      (setf command (parse-integer command))
      (setf command (or (second (assoc command *numeric-replies*))
                        command)))
    ;; Read parameters.
    (loop
       ;; Eat leading spaces.
       (do () ((or (>= offset (length line))
                   (not (eql (char line offset) #\Space))))
         (incf offset))
       (cond ((>= offset (length line)) (return))
             ((eql (char line offset) #\:)
              (push (subseq line (1+ offset)) parameters)
              (return))
             (t (let ((start offset))
                  (do () ((or (>= offset (length line))
                              (eql (char line offset) #\Space)))
                    (incf offset))
                  (push (subseq line start offset) parameters)))))
    (values prefix command (nreverse parameters))))

(defun parse-command (line)
  (let ((command-end nil)
        (rest-start nil)
        (rest-end nil))
    (dotimes (i (length line))
      (when (eql (char line i) #\Space)
        (setf command-end i
              rest-start i)
        (return)))
    (when rest-start
      ;; Eat leading spaces.
      (do () ((or (>= rest-start (length line))
                  (not (eql (char line rest-start) #\Space))))
        (incf rest-start)))
    (values (subseq line 1 command-end)
            (subseq line (or rest-start (length line)) rest-end))))

(defun send (stream control-string &rest arguments)
  "Buffered FORMAT."
  (declare (dynamic-extent argument))
  (write-sequence (apply 'format nil control-string arguments) stream))

(defvar *command-table* (make-hash-table :test 'equal))

(defmacro define-server-command (name lambda-list &body body)
  (let ((args (gensym)))
    `(setf (gethash ,(if (and (symbolp name) (not (keywordp name)))
                         (symbol-name name)
                         name)
                    *command-table*)
           (lambda (,(first lambda-list) ,args)
             (declare (system:lambda-name (irc-command ,name)))
             (destructuring-bind ,(rest lambda-list) ,args
               ,@body)))))

(define-server-command privmsg (from channel message)
  ;; ^AACTION [msg]^A is a /me command.
  (cond ((and (>= (length message) 9)
              (eql (char message 0) (code-char #x01))
              (eql (char message (1- (length message))) (code-char #x01))
              (string= "ACTION " message :start2 1 :end2 8))
         (format t "[~A]* ~A ~A~%" channel from
                 (subseq message 8 (1- (length message)))))
        (t (format t "[~A]<~A> ~A~%" channel from message))))

(defvar *known-servers*
  '((:freenode (213 92 8 4) 6667))
  "A list of known/named IRC servers.")

(defun resolve-server-name (name)
  (let ((known (assoc name *known-servers* :key 'symbol-name :test 'string-equal)))
    (cond (known
           (values (second known) (third known)))
          (t (error "Unknown server ~S~%" name)))))

(defclass irc-client (sys.graphics::text-window)
  ((command-process :reader irc-command-process)
   (receive-process :reader irc-receive-process)
   (connection :initform nil :accessor irc-connection)
   (display :accessor irc-display*)))

(defmethod initialize-instance :after ((instance irc-client))
  (let ((cmd (sys.int::make-process "IRC command"))
        (rcv (sys.int::make-process "IRC receive")))
    (setf (slot-value instance 'command-process) cmd)
    (setf (slot-value instance 'receive-process) rcv)
    (sys.int::process-preset cmd 'irc-top-level instance)
    (sys.int::process-preset rcv 'irc-receive instance)
    (sys.int::process-enable cmd)
    (sys.int::process-enable rcv)))

(defclass hacky-framebuffer-stream (sys.int::edit-stream sys.int::framebuffer-output-stream)
  ((irc :initarg :irc)))

(defmethod sys.int::stream-write-char :after (character (stream hacky-framebuffer-stream))
  (setf sys.graphics::*refresh-required* t))
(defmethod sys.int::stream-clear-between :after ((stream hacky-framebuffer-stream) start-x start-y end-x end-y)
  (setf sys.graphics::*refresh-required* t))
(defmethod sys.int::stream-read-char ((stream hacky-framebuffer-stream))
  (loop
     (let ((char (sys.graphics::fifo-pop (sys.graphics::text-window-buffer (slot-value stream 'irc)))))
       (when char (return char)))
     (sys.int::process-wait "User input"
                            (lambda ()
                              (not (sys.graphics::fifo-emptyp (sys.graphics::text-window-buffer (slot-value stream 'irc))))))))

(defun irc-receive (irc)
  (sys.graphics::with-window-streams irc
    (with-simple-restart (abort "Give up")
      (sys.int::process-wait "Awaiting connection" (lambda () (irc-connection irc)))
      (let ((connection (irc-connection irc))
            (display (irc-display* irc)))
        (loop (let ((line (read-line connection)))
                (multiple-value-bind (prefix command parameters)
                    (decode-command line)
                  (let ((fn (gethash command *command-table*)))
                    (cond (fn (let ((*standard-output* display))
                                (funcall fn prefix parameters)))
                          ((keywordp command)
                           (format display "[~A] -!- ~A~%" prefix (car (last parameters))))
                          ((integerp command)
                           (format display "[~A] ~D ~A~%" prefix command parameters))
                          (t (write-line line display)))))))))))

(defvar *top-level-commands* (make-hash-table :test 'equal))

(defmacro define-command (name (&key text connection channel nick) &body body)
  (let ((text-sym (or text (gensym)))
        (con-sym (or connection (gensym)))
        (chan-sym (or channel (gensym)))
        (nick-sym (or nick (gensym))))
  `(setf (gethash ',(string-upcase (string name))
                  *top-level-commands*)
         (lambda (,text-sym ,con-sym ,chan-sym ,nick-sym)
           (declare (system:lambda-name (irc-command ,name))
                    (ignorable ,text-sym ,con-sym ,chan-sym ,nick-sym))
           ,@body))))

(define-condition quit-irc () ())

(define-command quit (:text text :connection connection)
  (when connection
    (send connection "QUIT :~A~%" text))
  (signal 'quit-irc))

(define-command raw (:text text :connection connection)
  (when connection
    (write-string rest connection)
    (terpri connection)))

(define-command eval (:text text)
  (format t "[eval] ~A~%" text)
  (eval (read-from-string text))
  (fresh-line))

(define-command say (:text text :connection connection :channel channel :nick nick)
  (when (and connection channel)
    (format t "[~A]<~A> ~A~%" channel nick text)
    (send connection "PRIVMSG ~A :~A~%" channel text)))

(define-command me (:text text :connection connection :channel channel :nick nick)
  (when (and connection channel)
    (format t "[~A]* ~A ~A~%" current-channel nick text)
    (send connection "PRIVMSG ~A :~AACTION ~A~A~%"
          channel (code-char 1) text (code-char 1))))

(defun irc-top-level (irc)
  (sys.graphics::with-window-streams irc
    (unwind-protect
         (handler-case
             (let* ((main-fb (sys.graphics::window-frontbuffer irc))
                    (dims (array-dimensions main-fb))
                    (display-fb (make-array (list (- (first dims) 20) (second dims))
                                            :displaced-to main-fb
                                            :displaced-index-offset 0))
                    (display (make-instance 'hacky-framebuffer-stream
                                            :irc irc
                                            :framebuffer display-fb))
                    (input-fb (make-array (list 16 (second dims))
                                          :displaced-to main-fb
                                          :displaced-index-offset (* (- (first dims) 16) (second dims))))
                    (input (make-instance 'hacky-framebuffer-stream
                                          :irc irc
                                          :framebuffer input-fb)))
               (setf (irc-display* irc) display)
               (sys.int::%bitset (first dims) (second dims) 0 main-fb 0 0)
               (sys.int::%bitset 2 (second dims) #xFFD8D8D8 main-fb (- (first dims) 20) 0)
               (let ((current-channel nil)
                     (joined-channels '())
                     (connection nil)
                     (nick nil))
                 (loop (format input "~A] " (or current-channel ""))
                    (let ((line (read-line input)))
                      (sys.int::stream-move-to input 0 0)
                      (cond ((and (>= (length line) 1)
                                  (eql (char line 0) #\/)
                                  (not (and (>= (length line) 2)
                                            (eql (char line 1) #\/))))
                             (multiple-value-bind (command rest)
                                 (parse-command line)
                               (cond ((string-equal command "nick")
                                      (setf nick rest)
                                      (when connection
                                        (send connection "NICK ~A~%" nick)))
                                     ((string-equal command "connect")
                                      (cond ((not nick)
                                             (format display "Use /nick to set a nickname before connecting.~%"))
                                            ((irc-connection irc)
                                             (format display "Already connected.~%"))
                                            (t (multiple-value-bind (address port)
                                                   (resolve-server-name rest)
                                                 (setf connection (sys.net::tcp-stream-connect address port)
                                                       (irc-connection irc) connection)
                                                 (send (irc-connection irc) "USER ~A hostname servername :~A~%" nick nick)
                                                 (send (irc-connection irc) "NICK ~A~%" nick)))))
                                     ((string-equal command "join")
                                      (when connection
                                        (send connection "JOIN ~A~%" rest)
                                        (push rest joined-channels)
                                        (unless current-channel
                                          (setf current-channel rest))))
                                     ((string-equal command "chan")
                                      (when connection
                                        (setf current-channel rest)))
                                     ((string-equal command "part")
                                      (when (and connection current-channel)
                                        (send connection "PART ~A :~A~%" current-channel rest)
                                        (setf current-channel nil)))
                                     (t (let ((fn (gethash (string-upcase command) *top-level-commands*)))
                                          (if fn
                                              (with-simple-restart (abort "Abort evaulation and return to IRC.")
                                                (let ((*standard-input* input)
                                                      (*standard-output* display)
                                                      (*query-io* (make-two-way-stream input display)))
                                                  (funcall fn rest connection current-channel nick)))
                                              (format display "Unknown command ~S.~%" command)))))))
                            (current-channel
                             (format display "[~A]<~A> ~A~%" current-channel nick line)
                             (send connection "PRIVMSG ~A :~A~%" current-channel line)))))))
           (quit-irc ()))
      (sys.int::process-disable (irc-receive-process irc))
      (when (irc-connection irc)
        (close (irc-connection irc)))
      (sys.graphics::close-window irc))))

(defun create-irc-client ()
  "Open an IRC window."
  (sys.graphics::window-set-visibility (sys.graphics::make-window "Unconnected IRC" 640 400 'irc-client) t))

(setf (gethash (name-char "F2") sys.graphics::*global-keybindings*) 'create-irc-client)
